plugins {
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'scala'
    id 'com.gtnewhorizons.retrofuturagradle' version '1.4.1'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
}


version = "3.5.1+D"
group = "io.github.h2sxxa"
def modid = "scala3std"
archivesBaseName = modid
def modName = "Scala3Std"


java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
        // Azul covers the most platforms for Java 8 toolchains, crucially including MacOS arm64
        vendor.set(JvmVendorSpec.AZUL)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala', 'src/main/java']
        }
        java {
            srcDirs = []
        }
    }
}


idea {
    module {
        inheritOutputDirs = true
    }
}

compileScala {
    scalaCompileOptions.additionalParameters = ["-Xtarget", "8"] // scala/scala3#13810
}

compileJava {
    sourceCompatibility = 8 // for the IDE support

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}


minecraft {
    mcVersion.set('1.12.2')

    // MCP Mappings
    mcpMappingChannel = 'stable'
    mcpMappingVersion = '39'

    injectedTags.put('VERSION', project.version)
    injectedTags.put('MODID', modid)
    injectedTags.put('NAME', modName)
}

tasks.injectTags.configure {
    // Change Tags class' name here:
    outputClassName.set("${project.group}.${project.archivesBaseName}.ModInfo")
}

tasks.named("processIdeaSettings").configure {
    dependsOn("injectTags")
}

processResources {
    // This will ensure that this task is redone when the versions change
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'modname', modName
    inputs.property 'modid', modid


    // Replace various properties in mcmod.info and pack.mcmeta if applicable
    filesMatching(['mcmod.info', 'pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml']) { fcd ->
        // Replace version and mcversion
        fcd.expand('version': project.version,
                'mcversion': project.minecraft.version,
                'modname': modName,
                'modid': modid
        )
    }


}
repositories {
    maven {
        // RetroFuturaGradle
        name 'GTNH Maven'
        url 'https://nexus.gtnewhorizons.com/repository/public/'
        allowInsecureProtocol = true
        mavenContent {
            includeGroup 'com.gtnewhorizons'
            includeGroup 'com.gtnewhorizons.retrofuturagradle'
        }
    }
    mavenCentral()
    mavenLocal() // Must be last for caching to work
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    shade "org.scala-lang:scala-library:2.13.15" // no need to use j8 compat because of 2.13.x
    shade "org.scala-lang:scala3-library_3:3.5.1"
}


shadowJar {
    relocate 'cross.net.minecraftforge.fml.common', 'net.minecraftforge.fml.common'
    relocate 'cross.net.neoforged.fml.common', 'net.neoforged.fml.common'
    relocate 'cross.net.fabricmc.api', 'net.fabricmc.api'
    relocate 'cross.cpw.mods.fml.common', 'cpw.mods.fml.common'

    exclude 'net'
    exclude 'cpw'
    exclude 'cross'

    relocate 'scala.', 'scala3.'

    configurations = [project.configurations.shade]
}

tasks.named("build") {
    dependsOn(tasks.named("shadowJar"))
}

// This task is used to make `runClient` using `shadow jar` instead of `common jar`.
tasks.register('shadowCopy', Copy) {
    mustRunAfter(tasks.named("shadowJar"))

    from 'build/libs'
    into 'build/libs'
    rename { String fileName -> fileName.replace('-all', '-dev')
    }
    include("*-all.jar")
}

tasks.named("jar") {
    dependsOn(tasks.named("shadowJar"))
}

tasks.named("packageMcLauncher") {
    dependsOn(tasks.named("shadowCopy"))
}